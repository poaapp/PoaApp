buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    jcenter()
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {

        applicationId "com.poaapp.app.cloud"

        minSdkVersion 15
        targetSdkVersion 23

        versionName "2.5"
        if (System.getenv("BUILD_NUMBER") != null) {
            versionCode System.getenv("BUILD_NUMBER").toInteger()
            setProperty("archivesBaseName", "PoaApp-$versionName-$versionCode")
            println "##teamcity[buildNumber '$versionName($versionCode)']"
        } else {
            versionCode 1000000
        }
    }

    sourceSets {
        main {
            res.srcDirs = ['src/main/res',
                           'src/main/res-material']
        }
    }

    if (project.hasProperty("ACTOR_KEYSTORE")) {
        signingConfigs {
            release {
                storeFile file(ACTOR_KEYSTORE)
                storePassword ACTOR_KEYSTORE_PASS
                keyAlias ACTOR_KEYSTORE_ALIAS
                keyPassword ACTOR_KEYSTORE_ALIAS_PASS
            }
        }
    }

    buildTypes {
        release {
            if (project.hasProperty("ACTOR_KEYSTORE")) {
                signingConfig signingConfigs.release
            }

            ext.betaDistributionGroupAliases = "actor-testers"
        }
    }

    // Some technical configuration

    packagingOptions {
        exclude 'LICENSE.txt'
    }

    dexOptions {
        incremental true
        preDexLibraries false
        javaMaxHeapSize "4g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'im.actor:android-sdk:1.0.220'
    compile 'im.actor:android-google-push:1.0.220'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }
}
